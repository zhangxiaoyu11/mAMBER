* Poly Proline in Gas Phase
* By Ross Walker & Mark Williamson (SDSC)
*
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Read in Topology and parameter file for ALL22

    OPEN READ CARD UNIT 1 NAME "../../../../dat/chamber/top_all22_prot.inp"
    READ RTF CARD UNIT 1
    CLOSE UNIT 1

    OPEN READ CARD UNIT 2 NAME "../../../../dat/chamber/par_all22_prot.inp"
    READ PARAM CARD UNIT 2
    CLOSE UNIT 2
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Turn off CMAP - Needed since the default for all22 was changed
!                 in C35B1 (A dangerous change in my opinion since
!                 it effectively silently turns on CMAP but oh well.)
    SKIPE CMAP

! Generate Poly Proline using internal coordinates
    read sequ card
    **
    *
    5
    ALA PRO PRO PRO ALA

    generate aal first nter last cter
    autogenerate angles dihe

    ic gener
    ic param
    ic seed aal 1 N aal 1 CA aal 1 C
    ic build
    print coor

! No disulphide bonds to add.

    open unit 1 form write name poly_pro_gas_all22.psf
    write psf card unit 1
    close unit 1

!**********************************************************
!**********************************************************
!
!       Minimization
!
!**********************************************************
!**********************************************************

  update bycb  switch vswitch  -
         cutnb 9999.0   ctofnb 9998.0   ctonnb 9997.0 

  MINI SD NSTEP 200 TOLGRD 0.0001 NPRINT 10
  MINI CONJ NSTEP 200 TOLGRD 0.0001 NPRINT 10

  WRITE COOR pdb NAME 01_minimize.pdb

  WRITE COOR CARD NAME 01_minimize.crd

  stop
 
